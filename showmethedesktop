#!/usr/bin/env perl

use strict;
use warnings;

use FindBin;

BEGIN {
    use lib "$FindBin::Bin/contrib/protocol-rfb/lib";
    use lib "$FindBin::Bin/contrib/protocol-websocket/lib";
    use lib "$FindBin::Bin/contrib/event_reactor/lib";
    use lib "$FindBin::Bin/contrib/reanimator/lib";
}

use constant DEBUG => $ENV{SHOWMETHEDESKTOP_DEBUG} ? 1 : 0;

use JSON;
use ReAnimator;
use Protocol::RFB::Client;

use Getopt::Long 'GetOptions';
use File::Basename;

$SIG{INT} = $SIG{TERM} = sub { exit 0 };

my $HELP        = 0;
my $VNC_ADDRESS = 'localhost';
my $VNC_PORT    = 5900;
my $VNC_PASSWORD;
my $WIDTH;
my $HEIGHT;

GetOptions(
    "help|h"     => \$HELP,
    "address=s"  => \$VNC_ADDRESS,
    "port=s"     => \$VNC_PORT,
    "password=s" => \$VNC_PASSWORD,
    "width=i"    => \$WIDTH,
    "height=i"   => \$HEIGHT
);

my $basename = File::Basename::basename $0;
my $USAGE    = <<"EOF";
Usage: $basename --password <password>

Other options:

    --address   Default is '$VNC_ADDRESS'
    --port      Default is '$VNC_PORT'

    --width     Default is server's width
    --height    Default is server's height

EOF

die $USAGE if $HELP || !$VNC_PASSWORD;

ReAnimator->new(
    on_accept => sub {
        my ($self, $client) = @_;

        my $vnc = Protocol::RFB::Client->new(password => $VNC_PASSWORD);

        my $slave = $self->event_reactor->connect(
            address    => $VNC_ADDRESS,
            port       => $VNC_PORT,
            on_connect => sub {
                my $slave = shift;

                $slave->on_read(
                    sub {
                        my $slave = shift;
                        my $chunk = shift;

                        $vnc->parse($chunk);
                    }
                );

                $slave->on_disconnect(sub { $self->drop($client) });
            }
        );

        $vnc->on_handshake(
            sub {
                my $vnc = shift;

                warn 'Handshake is done' if DEBUG;

                $client->send_message(
                    JSON->new->encode(
                        {   type   => 's',
                            name   => $vnc->server_name,
                            width  => $WIDTH || $vnc->width,
                            height => $HEIGHT || $vnc->height
                        }
                    )
                );
            }
        );

        $vnc->on_framebuffer_update(
            sub {
                my ($vnc, $message) = @_;

                foreach my $rectangle (@{$message->rectangles}) {
                    $client->send_message(
                        JSON->new->encode(
                            {type => 'fu', rectangle => $rectangle}
                        )
                    );
                }
            }
        );

        $vnc->on_write(
            sub {
                my ($vnc, $chunk) = @_;

                $slave->write($chunk);
            }
        );

        $client->on_message(
            sub {
                my ($client, $message) = @_;

                warn "Message from client $message" if DEBUG;

                my $json = JSON->new;

                eval { $message = $json->decode($message); };
                return if !$message || $@;

                if ($message->{type} eq 'fuq') {
                    $vnc->framebuffer_update_request($message->{x},
                        $message->{y}, $message->{width}, $message->{height},
                        $message->{incremental});
                }
                elsif ($message->{type} eq 'pe') {
                    my $mask = 0;

                    if ($message->{event} eq 'mouseup') {
                        $mask = 0;
                    }
                    elsif ($message->{event} eq 'mousedown') {
                        $mask = 1;
                    }
                    elsif ($message->{event} eq 'mousemove') {
                        $mask = 0;
                    }
                    elsif ($message->{event} eq 'mousemove+mousedown') {
                        $mask = 1;
                    }
                    else {
                        warn 'Unknown pointer event';
                        return;
                    }

                    $vnc->pointer_event($message->{x}, $message->{y}, $mask);
                }
                elsif ($message->{type} eq 'ke') {
                    $vnc->key_event($message->{is_down}, $message->{key});
                }
            }
        );
    }
)->listen->start;
